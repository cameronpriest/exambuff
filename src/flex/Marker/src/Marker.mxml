<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:view="views.*" 
				backgroundGradientColors="[0xe5e5e5,0xf8f8f8]"
				 backgroundColor="0xfefefe"
				themeColor="0xf4d736" 				
				applicationComplete="_getAppData()"						
				>
		<mx:ArrayCollection id="helpTopics">
			<mx:Object label="Move the page" 
					   title="How to move the page image" />
			<mx:Object label="Commenting on the text" 
					   title="How to comment on text"/>
		</mx:ArrayCollection>
		<mx:Array id="helpText">
			<mx:Text condenseWhite="true">
				<mx:htmlText>
					<![CDATA[
<p align="left">To move the image:</p><ul><li>Press and hold space to enter panning mode</li><li>While in panning mode you can move the page image around by clicking and dragging the page with your mouse.</li><li>Release the space and you'll re-enter marking mode, and will be able to draw marking boxes again.</li></ul> 
					]]>
				</mx:htmlText>
			</mx:Text>
			<mx:Text condenseWhite="true">
				<mx:htmlText>
					<![CDATA[
<p align="left">To comment on the text:</p><ul><li>Press the mouse button down anywhere on the page</li><li>Drag the mouse; a yellow marking box will appear and resize as you drag the mouse</li><li>Release the mouse and the marking box will be placed. If you make a mistake, use the red 'X' to delete the box</li></ul> 
					]]>
				</mx:htmlText>
			</mx:Text>
		</mx:Array>
			
		<mx:ApplicationControlBar dock="true" paddingBottom="10" focusEnabled="false">
			<mx:HBox width="100%" id="appBar">
				<mx:Image id="myLogo" source="@Embed('/../img/logo-small-optimised.png')"/>
					<mx:Label width="80" text="Help topics:" bottom="{this.help.height}" />
					<mx:PopUpMenuButton focusEnabled="false" id="help" dataProvider="{this.helpTopics}" bottom="0" itemClick="{this.showHelp(event)}">
					</mx:PopUpMenuButton>
					<mx:Label>
						<mx:htmlText>
							<![CDATA[
								<b>Quick tip:</b> Press and hold space to enter panning mode, then click and drag the mouse to move the page around.
							]]>
						</mx:htmlText>
					</mx:Label>
			</mx:HBox>	
		</mx:ApplicationControlBar>
		<mx:Script>
			<![CDATA[
				import mx.events.CloseEvent;
				import mx.controls.Text;
				import mx.containers.TitleWindow;
				import views.LoginWindow;
				import events.MarksEvent;
				import events.ServerDataEvent;
				import mx.events.MenuEvent;
				import mx.events.FlexEvent;
				import mx.controls.Alert;
				import views.MarkingAreaView;
				import views.SubmissionView;
			
				import models.ScriptPages;
				import models.Marks;
				
				import helper.Downloader;
				import helper.JobLoader;
				
				import mx.controls.Image;
				import mx.automation.events.ListItemSelectEvent;	
				import mx.binding.utils.BindingUtils;
				import mx.events.ListEvent;
				import mx.managers.PopUpManager;
				
				import events.MarkingAreaEvent;
				import events.JobLoaderEvent;		
				import events.ScriptPagesEvent;
				import events.SubmissionViewEvent;
				
				import mx.managers.CursorManager;
				import helper.Key;
				
				/**
				 * App wide vars
				 * */ 
				// model to hold, save and load marks for scripts
				[Bindable]
				public var marks:Marks;
				
				[Bindable]	
				public var scriptPages:ScriptPages;
				
				private var _appBarControls:DisplayObjectContainer;
				
				private var _jobLoader:JobLoader;
				
				private var _markingView:MarkingAreaView;
				
				private var _submissionView:SubmissionView;
				
				private var _baseURL:String;
				private const _dataController:String = '/marker/assess';
				
				// store the total page count
				private var _lastPage:int;
				
				/**
				 * Get location data and apply it to objects that need it
				 * */
				private function _getAppData():void {
					trace('app complete');
					if(Application.application.loaderInfo.url.match('.+?exambuff.co.uk')) {
						this._baseURL = 'http://www.exambuff.co.uk';
					} else {
						this._baseURL = 'http://localhost:8080/CI/index.php';
					}
					this._jobLoader = new JobLoader(this._baseURL+this._dataController,true);
					this.scriptPages = new ScriptPages(this._baseURL+this._dataController);
					this.marks = new Marks(this._baseURL+this._dataController);
					this._init();
				}
				private function _init(e:Event = null):void {	
					trace('initialising');
					this._addLoadListeners();
					this._jobLoader.loadJobs();
					
					this.addEventListener(MarkingAreaEvent.FINISHED,_markAndSubmit);
				}
				private function _addLoadListeners():void {
					this._jobLoader.addEventListener(JobLoaderEvent.JOBS_LOADED,this._jobListLoaded);
					this._jobLoader.addEventListener(JobLoaderEvent.NO_JOBS,this._noJobs);
					this._jobLoader.addEventListener(JobLoaderEvent.NOT_AUTH,this._notAuth);
					this._jobLoader.addEventListener(JobLoaderEvent.MARKER_HAS_JOB,this._hasJob);
				}
				private function _removeLoadListeners():void {
					this._jobLoader.removeEventListener(JobLoaderEvent.JOBS_LOADED,this._jobListLoaded);
					this._jobLoader.removeEventListener(JobLoaderEvent.NO_JOBS,this._noJobs);
					this._jobLoader.removeEventListener(JobLoaderEvent.NOT_AUTH,this._notAuth);
					this._jobLoader.removeEventListener(JobLoaderEvent.MARKER_HAS_JOB,this._hasJob);
				}
				/**
				 * This is called if the marker is already marking a script
				 * */
				private function _hasJob(e:JobLoaderEvent):void {
					this._removeLoadListeners();
					trace('MRK Marker has job, loading...');
					Alert.show('You are already marking an essay. The essay is now being loaded.','You are already marking an essay');
					this._startMarkingJob(e.script,e.lastPage);
				}
				/**
				 * Gets jobs and loads them into job loader list
				 * */
				private function _jobListLoaded(e:JobLoaderEvent):void {
					this._removeLoadListeners();
					trace('Job list loaded');
					var jobLoader:JobLoader = JobLoader(e.target);
					this.jobList.dataProvider = jobLoader.jobs;
				}
				/**
				 * Informs the user if no jobs have been found
				 * */
				private function _noJobs(e:JobLoaderEvent):void {
					this._removeLoadListeners();
					this.jobList.dataProvider = null;
					Alert.show('No answers have been uploaded for marking. Please check back later','No answers found');
				}
				/**
				 * Informs the user if no jobs have been found
				 * */
				private function _notAuth(e:JobLoaderEvent):void {
					this._removeLoadListeners();
					var lw:LoginWindow = this._login();
					lw.addEventListener(LoginWindow.SUCCESS,this._init);
				}
				/**
				 * On select job, first check the job is still available and then listen for load success. 
				 * This is better done by main view, as if it fails we want to let the user try again, 
				 * without loading the MarkingAreaView.
				 * */
				private function _selectJob(event:MouseEvent):void {
					if(!this.jobList.selectedItem) return;
					this._jobLoader.addEventListener(JobLoaderEvent.SUCCESSFULLY_ASSIGNED_JOB,_loadAssignedJob);
					this._jobLoader.addEventListener(JobLoaderEvent.JOB_ALREADY_TAKEN,_loadFailed);
					this._jobLoader.takeJob(this.jobList.selectedItem.ID);
				}
				private function _loadAssignedJob(event:JobLoaderEvent = null):void {
					trace('MAV._loadAssignedJob');
					var script:int = this.jobList.selectedItem.ID;
					var lastPage:int = this._jobLoader.jobs[this.jobList.selectedIndex].pages;
					this._startMarkingJob(script,lastPage);
				}
				private function _startMarkingJob(script:int,lastPage:int):void {
					trace('MAV._startMarkingJob');
					this.marks.script = script;
					this._lastPage = lastPage;
					this.scriptPages.addEventListener(ScriptPagesEvent.PAGE_LOADED,_startMarkingArea,false,0,true);
					this.scriptPages.addEventListener(ScriptPagesEvent.PAGE_LOAD_FAILED,_loadFailed,false,0,true);
					this.scriptPages.loadPage(script,0);
					this.selectJob.enabled = false;
				}
				/**
				 * Allow user to retry
				 * */
				private function _loadFailed(e:Event):void {
					this.selectJob.enabled = true;
				}
				/**
				 * Switch the view to marking view
				 * */
				private function _startMarkingArea(e:ScriptPagesEvent):void {
					// remove the listeners, as MarkingAreaView controller is taking over from now
					this.scriptPages.removeEventListener(ScriptPagesEvent.PAGE_LOADED,_startMarkingArea);
					this.scriptPages.removeEventListener(ScriptPagesEvent.PAGE_LOAD_FAILED,_loadFailed);
					
					this.removeAllChildren();
					// view transition - remove old
					// set up new - binding the app wide marks and script pages
					this._markingView = new MarkingAreaView();
					this._markingView.addEventListener(FlexEvent.CREATION_COMPLETE,getAppBar,false,0,true);
					this._markingView.init(MarkingAreaView.MARKING_STATE);
					
					this._markingView.lastPage = this._lastPage;
					this._markingView.marks = this.marks;
					this._markingView.scriptPages = this.scriptPages;							
					this.addChild(this._markingView);
					// & start the marking view off, loading the first page
				}
				private function getAppBar(fe:FlexEvent):void {
					this.changeAppBar(fe.target.appBarControls);
				}
				/**
				 * Switch view to entering final mark and submission
				 * */
				private function _markAndSubmit(e:MarkingAreaEvent):void {
					this._markingView.visible = false;
					this._submissionView = new SubmissionView();
					this._submissionView.addEventListener(FlexEvent.CREATION_COMPLETE,getAppBar,false,0,true);
					this._submissionView.marks = this.marks;
					this._submissionView.addEventListener(SubmissionViewEvent.SUBMIT,_submit,false,0,true);
					this._submissionView.addEventListener(SubmissionViewEvent.BACK_TO_MARK,backToMarking,false,0,true);
					this.addChild(this._submissionView);
					this._submissionView.x = 0;
				}
				private function backToMarking(se:SubmissionViewEvent):void {
					this.removeChild(this._submissionView);
					this._submissionView = null;
					this.changeAppBar(this._markingView.appBarControls);
					this._markingView.visible = true;
				}
				private function _submit(e:Event = null):void {
					this.marks.addEventListener(ServerDataEvent.NEED_TO_LOGIN,this._saveLogin);
					this.marks.addEventListener(MarksEvent.MARKS_SAVED,this._saved);
					this.marks.save();
				}
				private function _saveLogin(ServerDataEvent:Event):void {
					var lw:LoginWindow = this._login();
					lw.addEventListener(LoginWindow.SUCCESS,this._submit,false,0,true);
				}
				/**
				 * Login window, which returns the object to attach event listeners to so the method
				 * can be generic, used by all methods that may require login.
				 * */
				private function _login():LoginWindow {
					var lw:LoginWindow = new LoginWindow();
					lw.addEventListener(LoginWindow.SUCCESS,_removeLoginWindow);
					lw.baseURL = this._baseURL;
					PopUpManager.addPopUp(lw,this,true);
					PopUpManager.centerPopUp(lw);
					return lw;
				}
				private function _removeLoginWindow(e:Event):void {
					PopUpManager.removePopUp(LoginWindow(e.target));
				}
				private function _saved(e:MarksEvent):void {
					navigateToURL(new URLRequest(this._baseURL+'/marker'),'_self')
					this.removeChild(this._markingView);
					this._markingView = null;
					this.marks = new Marks(this._baseURL);
					this.removeChild(this._submissionView);
					this._loadJobView();
				}
				private function _loadJobView():void {
					this.jobList.dataProvider = null;
					this.selectJob.enabled = true;
					this.scriptPages = new ScriptPages(this._baseURL);
					this._addLoadListeners();
					this._jobLoader.loadJobs();
					this.addChild(jobView);
				}
				private function changeAppBar(doc:DisplayObjectContainer):void {
					if(this._appBarControls) this.appBar.removeChild(this._appBarControls);
					this._appBarControls = doc;
					this.appBar.addChild(this._appBarControls);
				}
				private function showHelp(e:MenuEvent):void {
					var tw:TitleWindow = new TitleWindow();
					var text:Text = new Text();
					text.htmlText = this.helpText[e.index].htmlText;
					text.width = this.width * .4;
					
					tw.title = 'Help: '+this.helpTopics[e.index].title;
					tw.addChild(text);
					tw.showCloseButton = true;
					
					tw.addEventListener(CloseEvent.CLOSE,this.removeHelpItem,false,0,true);
					PopUpManager.addPopUp(tw,this,true);
					PopUpManager.centerPopUp(tw);
					tw.y -= this.height * .2;
				}
				private function removeHelpItem(ce:CloseEvent):void {
					PopUpManager.removePopUp(TitleWindow(ce.target));
				}
			]]>
		</mx:Script>		
		<mx:VBox id="jobView" width="80%">
			<mx:Label text="Answers to Mark" fontSize="24"/>
			<mx:DataGrid id="jobList" width="100%" >
				<mx:columns>
				    <mx:DataGridColumn dataField="question" headerText="Question" />
				    <mx:DataGridColumn dataField="subject" headerText="Subject"  />
				    <mx:DataGridColumn dataField="created" headerText="Created" />
				</mx:columns>
			</mx:DataGrid>
			<mx:Canvas width="100%">
				<mx:Button id="selectJob" label="Select Job" click="_selectJob(event)" right="0"/>
			</mx:Canvas>
		</mx:VBox>
</mx:Application>